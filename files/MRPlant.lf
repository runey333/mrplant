target C {
    threading: false,
    build: "../scripts/build_nrf_unix.sh"
};

import MoistureSetter from "lib/MoistureBLE.lf"
import Pump from "lib/Pump.lf"
import Moisture1, Moisture2 from "lib/SensorADC.lf"
import FSM from "lib/Fsm.lf"

preamble {=
    #include "nrf_saadc.h"
=}

main reactor {
    timer t(0, 1000 msec);
    state toggle:bool(false);
    state avgMoisture:int(0);
    ms = new MoistureSetter();
    fsm = new FSM(error = 10);
    ms1 = new Moisture1();
    ms2 = new Moisture2();
    pump = new Pump();

    reaction(t) -> ms1.trigger, ms2.trigger {=
        lf_set(ms1.trigger, self->toggle);
        lf_set(ms2.trigger, self->toggle);
        self->toggle = !self->toggle;
    =}
    
    reaction(ms1.moistureLvl, ms2.moistureLvl) -> fsm.moisture {=
        self->avgMoisture = (int)((ms1.moistureLvl->value+ms2.moistureLvl->value)/2);
        lf_set(fsm.moisture, self->avgMoisture);
        printf("Moisture level: %d\tMS1: %d\tMS2: %d\n", self->avgMoisture, ms1.moistureLvl->value, ms2.moistureLvl->value);
    =}

    reaction(fsm.water) -> pump.water {=
        lf_set(pump.water, fsm.water->value);
    =}
}


