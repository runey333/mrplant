target C;

preamble{=

    #include "nrfx_saadc.h"
    #include "nrf_gpio.h"
    
    
    #define ANALOG_PIN1 NRF_SAADC_INPUT_AIN1

    #define ANALOG_PIN2 NRF_SAADC_INPUT_AIN4

    int calibrationFunc1 (int val) {
        return (int)((val+200)/30); //(int)((val+200)/20);
    }
    int calibrationFunc2 (int val) {
        return (int)((val+250)/30); //(int)((val+250)/22);
    }

    void saadc_callback (nrfx_saadc_evt_t const * p_event) {
        // Ignore ADC callbacks.
    }

    nrf_saadc_value_t sample_value(uint8_t channel) {
        nrf_saadc_value_t val;
        ret_code_t error_code = nrfx_saadc_sample_convert(channel, &val);
        APP_ERROR_CHECK(error_code);
        return val;
    }
    

    bool moisture_sensor1_initialized = false;
    bool moisture_sensor2_initialized = false;
=}

reactor Moisture1 {
    input trigger:bool;
    output moistureLvl:nrf_saadc_value_t;

    reaction(startup){=
        if (moisture_sensor1_initialized) return;
        moisture_sensor1_initialized = true;

        nrfx_saadc_config_t saadc_config = NRFX_SAADC_DEFAULT_CONFIG;
        saadc_config.resolution = NRF_SAADC_RESOLUTION_12BIT;
        ret_code_t error_code = nrfx_saadc_init(&saadc_config, saadc_callback);
        APP_ERROR_CHECK(error_code);

        nrf_saadc_channel_config_t channel_config = NRFX_SAADC_DEFAULT_CHANNEL_CONFIG_SE(0);
        channel_config.gain         = NRF_SAADC_GAIN1_6;
        channel_config.reference    = NRF_SAADC_REFERENCE_INTERNAL;
        channel_config.pin_p        = ANALOG_PIN1;
        channel_config.acq_time     = NRF_SAADC_ACQTIME_40US;
        error_code = nrfx_saadc_channel_init(0, &channel_config);
        APP_ERROR_CHECK(error_code);
        printf("C %d, P %d\n", 0, channel_config.pin_p);
    =}

    reaction(trigger) -> moistureLvl{=
        if(trigger->value) {
            nrf_saadc_value_t moist = sample_value(0);
            lf_set(moistureLvl, calibrationFunc1(moist));
        }
    =}
}

reactor Moisture2 {
    input trigger:bool;
    output moistureLvl:nrf_saadc_value_t;

    reaction(startup){=
        if (moisture_sensor2_initialized) return;
        moisture_sensor2_initialized = true;

        nrf_saadc_channel_config_t channel_config = NRFX_SAADC_DEFAULT_CHANNEL_CONFIG_SE(1);
        channel_config.gain         = NRF_SAADC_GAIN1_6;
        channel_config.reference    = NRF_SAADC_REFERENCE_INTERNAL;
        channel_config.pin_p        = ANALOG_PIN2;
        channel_config.acq_time     = NRF_SAADC_ACQTIME_40US;
        ret_code_t error_code = nrfx_saadc_channel_init(1, &channel_config);
        APP_ERROR_CHECK(error_code);
        printf("C %d, P %d\n", 1, channel_config.pin_p);
    =}

    reaction(trigger) -> moistureLvl{=
        if(trigger->value) {
            nrf_saadc_value_t moist = sample_value(1);
            lf_set(moistureLvl, calibrationFunc2(moist));
        }
    =}
}
